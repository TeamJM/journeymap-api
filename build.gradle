/*
 * JourneyMap API (http://journeymap.info)
 * http://github.com/TeamJM/journeymap-api
 *
 * Copyright (c) 2011-2016 Techbrew.  All Rights Reserved.
 * The following limited rights are granted to you:
 *
 * You MAY:
 *  + Write your own code that uses the API source code in journeymap.* packages as a dependency.
 *  + Write and distribute your own code that uses, modifies, or extends the example source code in example.* packages
 *  + Fork and modify any source code for the purpose of submitting Pull Requests to the TeamJM/journeymap-api repository.
 *    Submitting new or modified code to the repository means that you are granting Techbrew all rights to the submitted code.
 *
 * You MAY NOT:
 *  - Distribute source code or classes (whether modified or not) from journeymap.* packages.
 *  - Submit any code to the TeamJM/journeymap-api repository with a different license than this one.
 *  - Use code or artifacts from the repository in any way not explicitly granted by this license.
 *
 */
/* Repos */
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {

    }
}

/* Gradle Plugins */
plugins {
    id 'net.neoforged.gradle.userdev' version '[7.0.5,)'
    id 'net.neoforged.gradle.mixin' version '[7.0.5,)'
}
apply plugin: 'eclipse'
apply plugin: 'maven-publish'


group 'info.journeymap'

/* Artifact Version */
version = project.minecraft_version + '-' + project.api_version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
}

/* ForgeGradle spec for Minecraft */
runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'info'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run/client')
    }

    server {
        programArgument '--nogui'
        workingDirectory project.file('run/server')
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${loader_version}"
//    runtimeOnly fg.deobf("curse.maven:journeymap-${project.jm_project_id}:${project.jm_file_id}")
}
/* Replace tokens in resources files */
processResources
        {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft_version

            // replace stuff in mcmod.info on copy
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version': project.version, 'mcversion': project.minecraft_version
            }

            // copy everything else
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

/* Reobfuscated jar for just the Example Mod */
jar {
    dependsOn classes
    from sourceSets.main.output
    include 'journeymap/**'
    manifest {
        attributes = [
                "Manifest-Version"      : "1.0",
                "FMLModType"            : "GAMELIBRARY",
                "Implementation-Title"  : project.title,
                "Implementation-Version": project.version,
                "Implementation-URL"    : project.git_website
        ]
    }
}

/* Jar for just the API - Stays deobfuscated so it can be added as a mod project dependency */
task modJar(type: Jar, dependsOn: classes) {
    mustRunAfter = ['jar']
    from sourceSets.main.output
    archiveClassifier.set('examplemod')
    include 'example/**'
    include 'mcmod.info'
    include 'assets/**'
    manifest {
        attributes = [
                "Manifest-Version"      : "1.0",
                "FMLModType"            : "GAMELIBRARY",
                "Implementation-Title"  : project.title_example,
                "Implementation-Version": project.version,
                "Implementation-URL"    : project.git_website

        ]
    }
}

/* Javadoc properties */
javadoc {
    title = project.title_javadoc
}

/* Jar for Javadocs */
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

/* Jar for Sources */
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

/* Additional artifacts produced by build task */
artifacts {
    archives jar
    archives modJar
    archives javadocJar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "JourneyMap"
            url = uri("https://jm.gserv.me/repository/maven-snapshots/")

            credentials {
                username = project.findProperty("journeymap.user") ?: System.getenv("JOURNEYMAP_USER")
                password = project.findProperty("journeymap.password") ?: System.getenv("JOURNEYMAP_PASSWORD")
            }

            version = project.version
        }
    }

    publications {
        maven(MavenPublication) {
            artifact jar
            artifact javadocJar
            artifact sourcesJar
        }
    }
}
